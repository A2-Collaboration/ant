#!/usr/bin/perl
use strict;
use warnings;
use Getopt::Long;

use Cwd qw(abs_path);
use File::Path qw(mkpath);
use File::Basename qw(basename);
use Text::ParseWords;
use POSIX;
use IPC::Open2;
use Data::Dumper;

my $filelist = '-';             # default STDIN
my $clean = 0;
my $backup = 0;
my $resubmit = 0;
my $help = 0;
my $jobtag = 'Ant';
my $QSUB_BIN = 'qsub';
my $ANT_BIN = which('Ant');
my $ANTMAPREDUCE_BIN = which('AntMapReduce');
my $ANT_ARGS = '-b';            # ensure batch mode
my $LOG_DIR = 'log';
my $OUT_DIR = 'root';
my $no_log = 0;
my $no_test = 0;
my $no_qsub = 0;
my $queue = 'dflt';
my $walltime = '2:00:00';       # dflt_short has 2h walltime
my $mapreduce_args = ''; # empty means no AntMapReduce call, otherwise arguments for pipe
my $checkrunning = 0;
my $killrunning = 0;

# parse options
Getopt::Long::Configure(qw(gnu_getopt));
GetOptions(
           'help|h' => \$help,
           'clean|c' => \$clean,
           'backup|b' => \$backup,
           'resubmit' => \$resubmit,
           'tag|t=s' => \$jobtag,
           'filelist=s' => \$filelist,
           'no_log' => \$no_log,
           'no_test' => \$no_test,
           'no_qsub' => \$no_qsub,
           'queue=s' => \$queue,
           'walltime=s' => \$walltime,
           'mapreduce=s' => \$mapreduce_args,
           'checkrunning' => \$checkrunning,
           'killrunning' => \$killrunning,
          ) or print_help();

if ($help) {
  print_help();
}

if ($clean | $backup | $resubmit && !($clean ^ $backup ^ $resubmit)) {
  die "Options --clean --backup --resubmit are mutually exclusive\n";
}

my $AntSubmitLogFile = 'AntSubmit.log';
my $AntSubmitLogJobs = "$AntSubmitLogFile.$jobtag.jobs";
my $timestampstr = strftime('%Y-%m-%d-%H-%M-%S',localtime);

# little extra functionality, needs $AntSubmitLogJobs
if ($checkrunning) {
  &check_running_jobs;
  exit;
} elsif ($killrunning) {
  &kill_running_jobs;
  exit;
}


my $Interrupted = 0;            # to ensure it has a value
$SIG{INT} = sub {
  $Interrupted++;
};

&main;

sub main {
  # check some things before submission
  die "Ant not found in PATH=$ENV{PATH}" unless defined $ANT_BIN;

  # read in additional Ant args
  if (-f "Antrc") {
    open(my $Antrc_fh, "<Antrc") or die "Can't open Antrc: $!";
    while (my $line = <$Antrc_fh>) {
      # remove leading and trailing whitespace
      $line =~ s/^\s+//;
      $line =~ s/\s+$//;
      next if $line eq '';
      next if $line =~ /^#/;
      die "Argument $line in Antrc does not start with -" unless $line =~ /^\-/;
      $ANT_ARGS .= " $line";
    }
    close $Antrc_fh;
  } elsif (@ARGV==0) {
    die "No Antrc found and no additional arguments for Ant provided\n";
  }

  # check if job file list exists
  if (-f $AntSubmitLogJobs) {
    if ($clean) {
      unlink $AntSubmitLogJobs;
    } elsif ($backup) {
      my $AntSubmitLogJobsBak = "$AntSubmitLogJobs.$timestampstr";
      rename $AntSubmitLogJobs,$AntSubmitLogJobsBak
        or die "Cannot move $AntSubmitLogJobs to $AntSubmitLogJobsBak: $!";
    } elsif ($resubmit) {
      print STDERR "Will append resubmitted jobs to existing $AntSubmitLogJobs\n";
    } else {
      die "The joblist $AntSubmitLogJobs already exists. Maybe use --clean or --backup?";
    }
  }

  # any additional options to Ant
  for my $arg (@ARGV) {
    die "Optional argument '$arg' does not start with -" unless $arg =~ /^\-/;
    $ANT_ARGS .= " $arg";
  }

  # look for replacements inside ant args
  my @ant_args_repl = ($ANT_ARGS =~ m/\{\d+\}/g);

  # read in the filelist, check for more errors
  open(my $filelist_fh, "<$filelist") or die "Can't open filelist $filelist: $!\n";
  my $n = 0;
  my @jobs;
  my %basenames;
 line:
  while (my $line = <$filelist_fh>) {
    $n++;
    # remove leading and trailing whitespace
    $line =~ s/^\s+//;
    $line =~ s/\s+$//;
    my @files;
    for my $inputfile (parse_line('\s+',0,$line)) {
      unless(-f $inputfile) {
        warn "Warning: Inputfile $inputfile does not exist, skipping.";
        next line;
      }
      push(@files, $inputfile);
    }

    next line if @files==0;

    # check for replacements
    my %replacements;           # use hash to have unique replacements
    for my $repl (@ant_args_repl) {
      # remove {...}, extract number, starting from 1
      my $num = substr($repl, 1, length($repl)-2);
      if ($num > @files) {
        warn "Warning: Requested replacement {$num} exceeds number of columns, skipping";
        next line;
      }
      $replacements{$repl} = $files[$num-1];
    }

    # obtain inputfiles as leftover from replacements
    my @inputfiles;
    for (my $i=0;$i<@files;$i++) {
      if (exists $replacements{'{'.($i+1).'}'}) {
        next;
      }
      my $inputfile = $files[$i];
      unless($inputfile =~ /\.(dat|dat\.xz|dat\.gz|root)$/) {
        warn "Warning: Inputfile $inputfile does not end with .dat[.xz|.gz] or .root, skipping";
        next line;
      }
      push(@inputfiles, $inputfile);
    }

    if (@inputfiles==0) {
      warn "Warning: No inputfiles left over after replacements, skipping.";
      next line;
    }

    my $basename = basename($inputfiles[0]);
    my $oldbasename = $basename;
    if (exists $basenames{$basename}) {
      $basename .= '_'.$basenames{$basename};
      warn "Basename '$oldbasename' already seen, changed to '$basename'";
    }
    $basenames{$oldbasename}++;

    my $runnumber = extract_runnumber($inputfiles[0]);
    unless(defined $runnumber) {
      # use line number by default
      $runnumber = $n;
    }
    push(@jobs, {
                 'inputfiles' => \@inputfiles,
                 'replacements' => \%replacements,
                 'jobnumber' => $runnumber,
                 'logfile' => "$LOG_DIR/".$jobtag."_$basename.log",
                 'outputfile' => "$OUT_DIR/".$jobtag."_$basename.root"
                }
        );
  }
  close $filelist_fh;

  die "No jobs to be submitted, nothing to do.\n" if @jobs==0;

  # check for resubmission mode
  if ($resubmit) {
    # we expect both directories to be there
    die "Resubmit mode requires '$LOG_DIR' dir to be existent" unless -d $LOG_DIR;
    die "Resubmit mode requires '$OUT_DIR' dir to be existent" unless -d $OUT_DIR;

    my @resubmit_jobs;
    my $nNoLogfile = 0;
    my $nNonZeroExit = 0;
    my $nNoOutputFileWritten = 0;
    my $nNoOutputFileFound=0;
  job:
    for my $job (@jobs) {
      if (!(-f $job->{outputfile})) {
        $nNoOutputFileFound++;
      } elsif (-f $job->{logfile}) {
        # search for exit code,
        # and magic string from Ant about outputfile
        my $jobExitCode = undef;
        my $foundJobSummary = 0;
        my $nLinesOfJob = 0;
        my $outputFileWritten=0;
        # read file from backwards, hopefully improves performance
        open(my $tac, "tac $job->{logfile} |") or die "Can't open tac on $job->{logfile}: $!";
        for my $lastline (<$tac>) {
          if ($lastline =~/^Job Exit Code: (\d+)/) {
            $jobExitCode = $1;
            next;
          }
          if ($foundJobSummary) {
            $nLinesOfJob++;
            # search for output file message from Ant
            my $outputFile = abs_path($job->{outputfile});
            if ($lastline =~ /Wrote output file $outputFile/) {
              $outputFileWritten = 1;
              last;
            }
            # give up at some point
            if ($nLinesOfJob>30) {
              last;
            }
          }
          if ($lastline =~ /^JOB SUMMARY$/) {
            $foundJobSummary=1;
            next;
          }
        }
        close $tac;

        my $jobExitCodeOk = defined($jobExitCode) && $jobExitCode == 0;
        $nNonZeroExit += !$jobExitCodeOk;
        $nNoOutputFileWritten += !$outputFileWritten;

        if ($jobExitCodeOk && $outputFileWritten) {
          next job;
        }
      } else {
        $nNoLogfile++;
      }
      # reached here, that means the job should be resubmitted
      # clean outputfile for safety, but keep logfile for later reference
      push(@resubmit_jobs, $job);
      if(!$no_qsub) {
        unlink $job->{outputfile};
      }
    }
    if (@resubmit_jobs == 0) {
      die "No jobs to be resubmitted found.\n";
    }
    print "Found ",(scalar @resubmit_jobs)," jobs to be resubmitted:\n",
      "$nNoOutputFileFound jobs without outputfile,\n",
      "$nNoLogfile jobs without logfile,\n",
      "$nNonZeroExit jobs with non-zero exit,\n",
      "$nNoOutputFileWritten jobs where Ant did not report the expected outputfile written\n";
    @jobs = @resubmit_jobs;
  } else {
    prepare_dir('outputfile', $OUT_DIR, \@jobs);
    prepare_dir('logfile',    $LOG_DIR, \@jobs);
  }


  # create items from jobs
  my @submissions;
  for my $job (@jobs) {
    push(@submissions, create_submission($job));
  }

  # test the first job
  if (!$no_test) {

    my $ant_cmd_test = $submissions[0]->[1];
    $ant_cmd_test .= ' -m 1 -b';
    print STDERR "Testing the first job locally...\n";
    open my $testpipe, "$ant_cmd_test |" or die "error running '$ant_cmd_test': $!";
    my @outputlines = <$testpipe>;
    close $testpipe;
    my $exitcode = $? >> 8;
    my $ant_outputfile_test = $jobs[0]->{outputfile};
    if ($exitcode != 0) {
      unlink $ant_outputfile_test;
      die "The test execution of '$ant_cmd_test' returned with non-zero exit code $exitcode:\n".join('',@outputlines);
    }

    unless(-f $ant_outputfile_test) {
      die "The test execution of '$ant_cmd_test' did not create the outputfile '$ant_outputfile_test'";
    }
    unlink $ant_outputfile_test;
  }

  # do not continue if already interrupted here
  exit 1 if $Interrupted;

  if ($no_qsub) {
    for my $sub (@submissions) {
      print STDOUT $sub->[1],"\n";
    }
  } else {
    # finally submit the jobs, remember the jobs in @submitted
    my $total = scalar @submissions;
    my @submitted;
    for (my $i=0;$i<$total;$i++) {
      last if $Interrupted;
      push(@submitted, [
                        abs_path($jobs[$i]->{outputfile}),
                        submit_job($submissions[$i])
                       ]
          );
      printf(STDERR "%-40s\r",sprintf("%04d/%04d = %03.0f %% submitted",scalar @submitted,
                                      $total,100*(scalar @submitted)/$total));
    }
    if ($Interrupted) {
      printf(STDERR "%-40s\n", 'Interrupted, killing '.(scalar @submitted).' already submitted jobs...');
      exit 1 if @submitted==0;
      # sleep a bit to give qstat a chance
      # it's complicated to cleanup all as qsub may not give jobid for last submitted job
      # we assume that qstat outputs jobs sequentially and should match our list after some offset
      # then we recover this missing jobid hopefully
      sleep 1;
      my @jobs_in_queue = &get_jobs_in_queue;
      if (@jobs_in_queue>0) {
        my $firstjobid = $submitted[0]->[1];
        if ($firstjobid ne '') {
          # search for offset in qstat output (there might be
          # other jobs running from same user)
          my $offset = -1;
          for (my $i=0;$i<@jobs_in_queue;$i++) {
            if ($firstjobid eq $jobs_in_queue[$i]) {
              $offset = $i;
              next;
            }
            if ($offset >= 0) {
              # $j is index in @submitted list
              my $j = $i-$offset;
              # are we looking at the last element in @submitted,
              # $i is pointing at the last element in @jobs_in_queue
              if ($j==@submitted-1 && $i == @jobs_in_queue-1 ) {
                # then fix the job id (if empty here)
                if ($submitted[$j]->[1] eq '') {
                  $submitted[$j]->[1] = $jobs_in_queue[$i];
                }
              }
              # might be that the job is completely missing, then we can't recover
              # the filename (but that's not a problem for killing jobs)
              elsif ($j == @submitted && $i == @jobs_in_queue-1) {
                push(@submitted, ['', $jobs_in_queue[$i]]);
              } else {
                # check job ids... die if unsuccessful (manual user intervention needed)
                if ($submitted[$j]->[1] ne $jobs_in_queue[$i]) {
                  die "Found mismatching job ids in queue and list of submitted. Can't cleanup jobs."
                }
              }
            }
          }
        } else {
          die "No jobid at all given by qsub, can't cleanup";
        }
      } else {
        warn "No jobs found in queue, although some submitted. Will try to kill anyway.";
      }
      # give qdel reversed list of jobids to kill queued jobs first
      system("qdel ".join(' ',map { $_->[1] } reverse @submitted)) == 0 or die "Can't kill jobs with qdel";
      exit 1;
    }

    printf(STDERR "%-40s\n", 'Submitted '.(scalar @submitted).' jobs.');

    # dump out the complete job file list
    # append job ids in resubmit mode
    my $mode = $resubmit ? '>>' : '>';
    open(my $jobfilelist, "$mode $AntSubmitLogJobs") or die "Can't open $AntSubmitLogJobs: $!";
    for my $sub (@submitted) {
      print $jobfilelist join(' ',@{$sub}),"\n";
    }
    close $jobfilelist;

    # run AntMapReduce if requested
    if ($mapreduce_args ne '') {
      die "No AntMapReduce found in PATH=$ENV{PATH}" unless defined $ANTMAPREDUCE_BIN;
      my $ANTMAPREDUCE_ARGS = "--filelist $AntSubmitLogJobs --tag=$jobtag $mapreduce_args";
      print STDERR "Executing AntMapReduce $ANTMAPREDUCE_ARGS\n";
      system("$ANTMAPREDUCE_BIN $ANTMAPREDUCE_ARGS") == 0 or die "Can't execute AntMapReduce: $!";
    }

    # log if not disabled
    if (!$no_log) {
      open(my $fh_log, ">>$AntSubmitLogFile") or die "Can't open $AntSubmitLogFile: $!";
      printf $fh_log "%s: %s: %04d jobs with args '%s'\n", $timestampstr, $jobtag, scalar @submitted, $ANT_ARGS;
      close $fh_log;
    }
  }
}

sub check_running_jobs {
  my $nRunning = scalar &get_running_jobs;
  $nRunning = 255 if $nRunning > 255;
  exit $nRunning;
}

sub kill_running_jobs {
  my @running_jobs = &get_running_jobs;
  if (scalar @running_jobs == 0) {
    die "No running jobs found for tag $jobtag\n";
  }
  system("qdel ".join(' ',reverse @running_jobs)) == 0 or die "Can't kill jobs with qdel";
  print STDERR "Killed ",(scalar @running_jobs)," running jobs for tag $jobtag\n";
}

sub get_running_jobs {
  open(my $joblistfile, "<$AntSubmitLogJobs") or die "Can't open job list $AntSubmitLogJobs: $!";
  my %jobids;
  while (my $line = <$joblistfile>) {
    chomp $line;
    my(undef, $jobid) = split(/\s+/,$line);
    $jobids{$jobid} = 1;
  }
  close $joblistfile;
  die "No jobids found in $AntSubmitLogJobs" if scalar keys %jobids == 0;

  my @jobs_in_queue = &get_jobs_in_queue;
  my @running_jobs;
  for my $jobid (@jobs_in_queue) {
    if ($jobids{$jobid}) {
      push(@running_jobs, $jobid);
    }
  }
  return @running_jobs;
}


sub get_jobs_in_queue {
  open(my $qstat, "qstat |") or die "Can't open qstat: $!";
  my @jobs_in_queue;
  while (my $line = <$qstat>) {
    chomp $line;
    my @cols  = split(/\s+/,$line);
    next if @cols != 6;         # ignore header/other lines
    my $user = $cols[2];
    next if $user ne $ENV{USER};
    my $jobid = $cols[0];
    push(@jobs_in_queue, $jobid);
  }
  close $qstat;
  return @jobs_in_queue;
}

sub prepare_dir {
  my $tag = shift;
  my $dir = shift;
  my @jobs = @{shift()};

  unless(-d $dir) {
    mkpath([$dir]) == 1 or die "Cannot create folder '$dir'";
  } else {

    my $backupdir = $dir.'_'.$timestampstr;
    if ($backup) {
      if (-d $backupdir) {
        die "Backupdir '$backupdir' already exists";
      }
      mkpath([$backupdir]) == 1 or die "Cannot create folder '$backupdir'";
    }

    my $n_files = 0;
    for my $job (@jobs) {
      my $file = $job->{$tag};
      if (-f $file) {
        if ($clean) {
          unlink $file or die "Cannot delete file $file: $!";
        } elsif ($backup) {
          my $basename = basename($file);
          my $backupfile = "$backupdir/$basename";
          rename $file,$backupfile or die "Cannot move $file to $backupfile: $!";
        } else {
          die "File $file already exists. Use --clean, --backup or --resubmit?\n";
        }
        $n_files++;
      }
    }
    if ($clean) {
      printf(STDERR "%04d %s cleaned\n", $n_files, $tag.'s', );
    } elsif ($backup) {
      printf(STDERR "%04d %s backuped to %s\n", $n_files, $tag.'s', $backupdir);
    }
  }

}

sub extract_runnumber {
  my $f = shift;
  if ($f =~ /_(\d+?)\.dat/) {
    return $1;
  }
  return undef;
}

sub which {
  my $cmd = shift;
  open(my $p, "which $cmd |") or die "Can't open which: $!";
  my @lines = <$p>;
  close $p;
  my $exit_value = $? >> 8;
  if ($exit_value != 0 || @lines != 1) {
    return undef;
  }
  chomp $lines[0];
  return $lines[0];
}

sub create_submission {
  my $job = shift;
  my $jobnumber = $job->{jobnumber};

  my $logfile = abs_path($job->{logfile});
  my $user = $ENV{USER};

  my $ant_args = $ANT_ARGS;
  my $repl = $job->{replacements};
  for my $key (keys %{$repl}) {
    my $subst = $repl->{$key};
    if (-f $subst) {
      $subst = abs_path($subst);
    }
    $ant_args =~ s/\Q$key\E/$subst/g;
  }

  my $ant_cmd = "$ANT_BIN $ant_args";

  for my $inputfile (@{$job->{inputfiles}}) {
    $inputfile = abs_path($inputfile);
    $ant_cmd .= " -i '$inputfile'";
  }
  my $outputfile = abs_path($job->{outputfile});
  $ant_cmd .= " -o '$outputfile'";

  my $qsub_cmd = $QSUB_BIN;

  $qsub_cmd .= " -N '$jobtag/$jobnumber'";

  # mails when job aborts
  $qsub_cmd .= " -m a";         # begin -m b, ends -m e
  $qsub_cmd .= " -M '$user\@kph.uni-mainz.de'";

  # logging: combine STDERR and STDOUT
  $qsub_cmd .= " -j oe -o '$logfile'";

  # misc stuff: batch quque, -V take over environment, walltime...
  $qsub_cmd .= " -q $queue -V -l ncpus=1 -l walltime=$walltime";

  return [$qsub_cmd, $ant_cmd];
}

sub submit_job {
  my $submission = shift;
  my $qsub_cmd = $submission->[0];
  my $ant_cmd = $submission->[1];
  # open pipe to qsub
  my($qsub_out,$qsub_in);
  my $qsub_pid = open2($qsub_out, $qsub_in, $qsub_cmd);
  print $qsub_in <<__BATCHSCRIPT;
echo QSUB_CMD="$qsub_cmd"
echo ANT_CMD="$ant_cmd"
$ant_cmd
__BATCHSCRIPT
  close $qsub_in;
  my $jobid = <$qsub_out>;
  return '' unless defined $jobid;
  chomp $jobid;
  if ($jobid =~ /\./) {
    # strip farm.kph string, to match qstat output, sigh
    $jobid = join('.',(split /\./, $jobid)[0..1]);
  }
  return $jobid;
}



sub print_help {
  print <<__EOF;
Usage: AntSubmit [--clean|--backup] [--tag jobtag] [--filelist filename] -- [optional Ant arguments]

Submit jobs running Ant on files in read from STDIN (or from file list
specified by --filelist), output goes to current directory. Reads
Antrc for arguments to Ant. You may use {n}, where n denotes the
column, inside the Ant argument. This string is replaced then by the
n-th column of given files, and not used as input file to Ant.

Options:

  --clean     Delete <outputdir> (and logs) before submission.
  --backup    Rename <outputdir> (and logs) to timestamped folder before submission.
  --resubmit  Resubmit jobs which have no log file or non-zero exit code
  --tag       Specify a custom job tag, default is Ant
  --filelist  Read from file instead of STDIN
  --no_log    Do not write submission into AntSubmit.log
  --no_test   Do not run first job as test before submission
  --no_qsub   Do not submit, but dumb Ant commands on STDOUT
  --queue     Set job queue (default: 'dflt')
  --walltime  Set walltime (default: 2:00:00, for dflt_short)
  --mapreduce Subsequently run AntMapReduce with joblist,
              supply arguments by option

  --checkrunning   returns exit code equal to number of running jobs matching --tag (max 255)
  --killrunning    deletes all jobs in queue (no matter what state) matching --tag
__EOF
  exit 255;
}
